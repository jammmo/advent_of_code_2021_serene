include input1.sn

function part1(input: Array of Int64) -> Int64 {
    var increases = 0

    for (i = 1, input.length()) {
        if (input[i] > input[i - 1]) {
            set increases += 1
        }
    }

    return increases
}

function part2(input: Array of Int64) -> Int64 {
    var window = Array(input[0], input[1], input[2])
    var last_sum = window[0] + window[1] + window[2]

    var increases = 0

    for (i = 3, input.length()) {
        var sum = 0
        set window[i % 3] = input[i]
        
        for (x in window) {
            set sum += x
        }

        if (sum > last_sum) {
            set increases += 1
        }

        set last_sum = sum
    }

    return increases
}

function main() {
    const input = getInput()

    print "Part One: ", part1(input)
    print "Part Two: ", part2(input)
}
